Copyright>        OpenRadioss
Copyright>        Copyright (C) 1986-2025 Altair Engineering Inc.
Copyright>
Copyright>        This program is free software: you can redistribute it and/or modify
Copyright>        it under the terms of the GNU Affero General Public License as published by
Copyright>        the Free Software Foundation, either version 3 of the License, or
Copyright>        (at your option) any later version.
Copyright>
Copyright>        This program is distributed in the hope that it will be useful,
Copyright>        but WITHOUT ANY WARRANTY; without even the implied warranty of
Copyright>        MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
Copyright>        GNU Affero General Public License for more details.
Copyright>
Copyright>        You should have received a copy of the GNU Affero General Public License
Copyright>        along with this program.  If not, see <https://www.gnu.org/licenses/>.
Copyright>
Copyright>
Copyright>        Commercial Alternative: Altair Radioss Software
Copyright>
Copyright>        As an alternative to this open-source version, Altair also offers Altair Radioss
Copyright>        software under a commercial license.  Contact Altair to discuss further if the
Copyright>        commercial version may interest you: https://www.altair.com/radioss/.
!||====================================================================
!||    m2cplr      ../engine/source/materials/mat/mat002/m2cplr.F
!||--- called by ------------------------------------------------------
!||    sigeps02c   ../engine/source/materials/mat/mat002/sigeps02c.F
!||====================================================================
      SUBROUTINE M2CPLR(NEL     ,EZZ    ,OFF      ,PLA      ,
     2                  IPLA    ,TEMP   ,C3       ,C4       ,M_EXP   ,
     3                  IFORM   ,ETSE   ,GS       ,EPSP     ,VP      ,
     4                  ISRATE  ,YLD    ,G        ,A1       ,A2      ,
     5                  NU      ,CA0    ,CB0      ,CN       ,YMAX0   ,
     6                  EPCHK   ,YOUNG  ,CC       ,EPDR     ,ICC     ,
     7                  DPLA    ,TSTAR  ,FISOKIN  ,GAMA_IMP ,SIGNOR  ,
     8                  HARDM   ,DEPSXX ,DEPSYY   ,DEPSXY  ,
     9                  DEPSYZ  ,DEPSZX ,SIGNXX   ,SIGNYY   ,SIGNXY  ,
     A                  SIGNYZ  ,SIGNZX ,SIGBAKXX ,SIGBAKYY ,SIGBAKXY,
     B                  SIGOXX  ,SIGOYY ,SIGOXY   ,SIGOYZ   ,SIGOZX  )
C-----------------------------------------------
C   I m p l i c i t   T y p e s
C-----------------------------------------------
#include      "implicit_f.inc"
C-----------------------------------------------
C   G l o b a l   P a r a m e t e r s
C-----------------------------------------------
#include      "mvsiz_p.inc"
C-----------------------------------------------
C   C o m m o n   B l o c k s
C-----------------------------------------------
#include      "impl1_c.inc"
C-----------------------------------------------
C   D u m m y   A r g u m e n t s
C-----------------------------------------------
      INTEGER ,INTENT(IN) :: NEL
      INTEGER ,INTENT(IN) :: IPLA
      INTEGER ,INTENT(IN) :: IFORM
      INTEGER ,INTENT(IN) :: ISRATE
      INTEGER ,INTENT(IN) :: ICC
      INTEGER ,INTENT(IN) :: VP
      my_real ,INTENT(IN) :: C3,C4
      my_real ,INTENT(IN) :: M_EXP
      my_real ,INTENT(IN) :: G
      my_real ,INTENT(IN) :: NU
      my_real ,INTENT(IN) :: A1
      my_real ,INTENT(IN) :: A2
      my_real ,INTENT(IN) :: CA0
      my_real ,INTENT(IN) :: CB0
      my_real ,INTENT(IN) :: CN
      my_real ,INTENT(IN) :: YMAX0
      my_real ,INTENT(IN) :: YOUNG
      my_real ,INTENT(IN) :: CC
      my_real ,INTENT(IN) :: EPDR
      my_real ,INTENT(IN) :: FISOKIN
      my_real, DIMENSION(NEL)  ,INTENT(IN)    :: DEPSXX,DEPSYY,DEPSXY,DEPSYZ,DEPSZX
      my_real, DIMENSION(NEL)  ,INTENT(IN)    :: SIGOXX,SIGOYY,SIGOXY,SIGOYZ,SIGOZX
      my_real, DIMENSION(NEL)  ,INTENT(IN)    :: GS
      my_real, DIMENSION(NEL)  ,INTENT(IN)    :: TSTAR
      my_real, DIMENSION(NEL)  ,INTENT(INOUT) :: SIGNXX,SIGNYY,SIGNXY,SIGNYZ,SIGNZX
      my_real, DIMENSION(NEL)  ,INTENT(INOUT) :: SIGBAKXX,SIGBAKYY,SIGBAKXY,HARDM
      my_real, DIMENSION(NEL)  ,INTENT(INOUT) :: EZZ,OFF,PLA,ETSE,EPCHK,YLD,EPSP
      my_real, DIMENSION(NEL)  ,INTENT(INOUT) :: TEMP,DPLA,GAMA_IMP
      my_real, DIMENSION(NEL,5),INTENT(INOUT) :: SIGNOR
C-----------------------------------------------
C   L o c a l   V a r i a b l e s
C-----------------------------------------------
      INTEGER :: I,J,N,NINDX,NMAX
      INTEGER :: INDEX(MVSIZ)
      my_real :: F,DF,PLA_I,P2,Q2,R,S1,S2,S3,YLD_I,SMALL,BETA,AA,AAA,HKIN,ALPHA         
      my_real :: A(MVSIZ), B(MVSIZ) ,DPLA_I(MVSIZ), DPLA_J(MVSIZ), DR(MVSIZ),
     .   H(MVSIZ), NU1(MVSIZ), NU2(MVSIZ)   ,P(MVSIZ)     ,Q(MVSIZ), 
     .   SVM(MVSIZ),CA(MVSIZ), CB(MVSIZ)    ,YMAX(MVSIZ)  ,
     .   SVM2(MVSIZ),YLD2(MVSIZ),HI(MVSIZ),HK(MVSIZ),LOGEP(MVSIZ),
     .   NU11(MVSIZ),NU21(MVSIZ),ANU1(MVSIZ),BNU2(MVSIZ),H2(MVSIZ)
      DATA NMAX/3/      
!===============================================================================
      NINDX = 0
      SMALL = EM7
      DO I = 1,NEL
        ETSE(I) = ONE
        CA(I)   = CA0
        CB(I)   = CB0 
        YMAX(I) = YMAX0 
        H(I)    = ZERO
!
        SIGNXX(I)=SIGOXX(I)
        SIGNYY(I)=SIGOYY(I)
        SIGNXY(I)=SIGOXY(I)
        SIGNYZ(I)=SIGOYZ(I)
        SIGNZX(I)=SIGOZX(I)
      ENDDO
C------------------------------------------
C     KINEMATIC HARDENING
C------------------------------------------
      IF (FISOKIN > ZERO) THEN
        DO I = 1,NEL
          SIGNXX(I)=SIGNXX(I)-SIGBAKXX(I)
          SIGNYY(I)=SIGNYY(I)-SIGBAKYY(I)
          SIGNXY(I)=SIGNXY(I)-SIGBAKXY(I)
        ENDDO
      END IF ! FISOKIN > 0
C---------------------------
C     CONTRAINTES ELASTIQUES
C---------------------------
      DO I = 1,NEL
        SIGNXX(I)=SIGNXX(I)+A1*DEPSXX(I)+A2*DEPSYY(I)
        SIGNYY(I)=SIGNYY(I)+A2*DEPSXX(I)+A1*DEPSYY(I)
        SIGNXY(I)=SIGNXY(I)+G*DEPSXY(I)
        SIGNYZ(I)=SIGNYZ(I)+GS(I)*DEPSYZ(I)
        SIGNZX(I)=SIGNZX(I)+GS(I)*DEPSZX(I)
      ENDDO
      DO I = 1,NEL
        LOGEP(I) = ZERO
      ENDDO
C-------------
C     STRAIN RATE (JOHNSON-COOK, ZERILLI-ARMSTRONG)
C-------------
      IF (CC /= ZERO) THEN
        IF (IFORM == 0) THEN 
          DO I = 1,NEL
            IF (ISRATE == 0.AND.VP == 2) EPSP(I) = 
     .           MAX( ABS(DEPSXX(I)), ABS(DEPSYY(I)), HALF*ABS(DEPSXY(I)))
            EPSP(I)  = MAX(EPSP(I),EPDR)
            LOGEP(I) = LOG(EPSP(I)/EPDR)
          ENDDO
#include "vectorize.inc"
          DO I = 1,NEL
            IF (TSTAR(I) == ZERO) THEN
              Q(I) = (ONE + CC * LOGEP(I))
            ELSE
              Q(I) = (ONE + CC * LOGEP(I))*(ONE-EXP(M_EXP*LOG(TSTAR(I))))
            ENDIF
            Q(I) = MAX(Q(I),EM20)
            CA(I) = CA(I) * Q(I)
            CB(I) = CB(I) * Q(I)
            IF (ICC == 1) YMAX(I) = YMAX(I) * Q(I)
          ENDDO
        ELSEIF (IFORM == 1) THEN   ! Zerilli
          DO I = 1,NEL
            IF (ISRATE == 0 .AND. VP == 2) EPSP(I) = MAX( ABS(DEPSXX(I)),
     .                   ABS(DEPSYY(I)), HALF*ABS(DEPSXY(I)))
            EPSP(I) = MAX(EPSP(I),EM20)
            LOGEP(I) = LOG(EPSP(I)/EPDR)
          ENDDO
          DO I = 1,NEL          
            Q(I) = LOGEP(I)
            Q(I) = CC*EXP((-C3 + C4*Q(I))*TEMP(I))
            IF (ICC == 1) YMAX(I) = YMAX(I) + Q(I)
            CA(I) = CA(I) + Q(I)
          ENDDO
        ENDIF ! IFORM
      ELSE           ! CC = ZERO  , no strain rate effect
        IF (IFORM == 0) THEN       ! J-C
          DO I = 1,NEL          
            IF (TSTAR(I) /= ZERO) THEN
              Q(I)  = ONE - EXP(M_EXP*LOG(TSTAR(I)))
              Q(I)  = MAX(Q(I),EM20)
              CA(I) = CA(I) * Q(I)
              CB(I) = CB(I) * Q(I)
            END IF
          ENDDO
        ENDIF ! IFORM
      ENDIF   ! IF (CC /= ZERO)
C-----------------------------------
C     YIELD
C-----------------------------------
      DO I = 1,NEL
        DPLA(I) = ZERO
        IF (PLA(I) == ZERO) THEN
           YLD(I)= CA(I)
        ELSE
           BETA = CB(I)*(ONE-FISOKIN)
           YLD(I)= CA(I)+BETA*EXP(CN*LOG(PLA(I)))
        ENDIF
        YLD(I)= MIN(YLD(I),YMAX(I))
      ENDDO
C------------------------------------------
C     CONTRAINTES PLASTIQUEMENT ADMISSIBLES
C------------------------------------------
      IF (IPLA == 0) THEN
C-------------------
C     CONTRAINTE VM
C-------------------
        DO I = 1,NEL
          SVM(I)=SQRT(SIGNXX(I)*SIGNXX(I)
     .               +SIGNYY(I)*SIGNYY(I)
     .               -SIGNXX(I)*SIGNYY(I)
     .         +THREE*SIGNXY(I)*SIGNXY(I))
        ENDDO
C radial projection 
#include "vectorize.inc" 
        DO I = 1,NEL
          R = MIN(ONE,YLD(I)/(SVM(I)+EM15))
          IF (R < ONE) THEN 
            SIGNXX(I)=SIGNXX(I)*R
            SIGNYY(I)=SIGNYY(I)*R
            SIGNXY(I)=SIGNXY(I)*R
            DPLA(I) =  OFF(I) * MAX(ZERO,(SVM(I)-YLD(I))/YOUNG)
            S1=HALF*(SIGNXX(I)+SIGNYY(I))
            EZZ(I) = DPLA(I) * S1 /YLD(I)
            PLA(I) = PLA(I) + DPLA(I)
            EPCHK(I) = MAX(PLA(I),EPCHK(I))     
            IF (YLD(I) >=YMAX(I)) THEN
              H(I)=ZERO
            ELSE
              H(I)=CN*CB(I)*EXP((CN-ONE)*LOG(PLA(I)+SMALL))
            ENDIF
            ETSE(I)= H(I)/(H(I)+YOUNG)
          ENDIF
        ENDDO
C------------------------------------------------------------------------
      ELSEIF (IPLA == 1) THEN
C-------------------------
C     von mises criterion
C-------------------------
        DO I = 1,NEL
          S1=SIGNXX(I)+SIGNYY(I)
          S2=SIGNXX(I)-SIGNYY(I)
          S3=SIGNXY(I)
          A(I)=FOURTH*S1*S1
          B(I)=THREE_OVER_4*S2*S2+THREE*S3*S3
          SVM(I)=SQRT(A(I)+B(I))  
        ENDDO
C-------------------------
C     GATHER PLASTIC FLOW
C-------------------------
        NINDX=0
        DO I = 1,NEL
          IF (SVM(I) > YLD(I) .AND. OFF(I) == ONE) THEN
            NINDX=NINDX+1
            INDEX(NINDX)=I
          ENDIF
        ENDDO
        IF (NINDX == 0) THEN
          IF (FISOKIN > ZERO) THEN
            DO I = 1,NEL
              SIGNXX(I)=SIGNXX(I) + SIGBAKXX(I)
              SIGNYY(I)=SIGNYY(I) + SIGBAKYY(I)
              SIGNXY(I)=SIGNXY(I) + SIGBAKXY(I)
            ENDDO
          END IF !(FISOKIN > ZERO) THEN
          IF (IMPL_S > 0.AND.IKT > 0) THEN
            DO I = 1,NEL
             GAMA_IMP(I) = ZERO
            END DO
          END IF !(IMPL_S > 0.AND.IKT > 0) THEN
          RETURN
        END IF !(NINDX==0) THEN
C---------------------------
C    DEP EN CONTRAINTE PLANE
C---------------------------
#include "vectorize.inc" 
        DO  J=1,NINDX
          I=INDEX(J)
          NU1(I)=ONE/(ONE-NU)
          NU2(I)=ONE/(ONE+NU)
          IF (YLD(I) >= YMAX(I)) THEN
            H(I)=ZERO
          ELSE
            H(I)=CN*CB(I)*EXP((CN-ONE)*LOG(PLA(I)+SMALL))
          ENDIF
          DPLA_J(I)=(SVM(I)-YLD(I))/(THREE*G+H(I))
          ETSE(I)= H(I)/(H(I)+YOUNG)
          ANU1(I)  = A(I)*NU1(I)
          BNU2(I) = THREE*B(I)*NU2(I)
          H2(I)= TWO*H(I)
        ENDDO
C-----just keep at least old performance--to update H(I) inside iteration for implicit+ETSE---------
        IF (FISOKIN == ZERO) THEN
          DO N=1,NMAX
#include "vectorize.inc" 
            DO J=1,NINDX
              I=INDEX(J)
              DPLA_I(I)=DPLA_J(I)
              PLA_I =PLA(I)+DPLA_I(I)
              DPLA(I) = DPLA_J(I)
              IF (PLA_I == ZERO) THEN
                YLD_I =MIN(YMAX(I),CA(I))
              ELSE
                YLD_I =MIN(YMAX(I),CA(I)+CB(I)*EXP(CN*LOG(PLA_I)))
              ENDIF
              DR(I) =HALF*YOUNG*DPLA_I(I)/YLD_I
              P(I)  =ONE/(ONE+DR(I)*NU1(I))
              Q(I)  =ONE/(ONE+THREE*DR(I)*NU2(I))
              P2    =P(I)*P(I)
              Q2    =Q(I)*Q(I)
              F     =A(I)*P2+B(I)*Q2-YLD_I*YLD_I
              DF    =-(ANU1(I)*P2*P(I)+BNU2(I)*Q2*Q(I))
     .               *(YOUNG-DR(I)*H2(I))/YLD_I
     .                -H2(I)*YLD_I
              IF (DPLA_I(I) > ZERO) THEN
                DPLA_J(I)=MAX(ZERO,DPLA_I(I)-F/DF)
              ELSE
                DPLA_J(I)=ZERO
              ENDIF        
            ENDDO ! DO J=1,NINDX
          ENDDO ! DO N=1,NMAX
!
        ELSE   ! FISOKIN > 0 -> kinematic/mix hardening 
#include "vectorize.inc" 
          DO  J=1,NINDX
            I=INDEX(J)
            BETA = H(I)*FISOKIN
            HI(I) = H(I)-BETA
            HK(I) = TWO_THIRD*BETA
            AAA = THREE*HK(I)/YOUNG
            NU11(I) = NU1(I) + AAA 
            NU21(I) = THREE*NU2(I) + AAA 
            ANU1(I) = A(I)*NU11(I)
            BNU2(I) = B(I)*NU21(I)
            H2(I)= TWO*HI(I)
          ENDDO
!
          DO N=1,NMAX
#include "vectorize.inc" 
            DO J=1,NINDX
              I=INDEX(J)
              DPLA_I(I)=DPLA_J(I)
              PLA_I =PLA(I)+DPLA_I(I)
              DPLA(I) = DPLA_J(I)
              BETA = ONE-FISOKIN
              IF (PLA_I == ZERO) THEN
                YLD_I =MIN(YMAX(I),CA(I))
              ELSE
                YLD_I =MIN(YMAX(I),CA(I)+BETA*CB(I)*EXP(CN*LOG(PLA_I)))
              ENDIF
              DR(I) =HALF*YOUNG*DPLA_I(I)/YLD_I
              P(I)  =ONE/(ONE+DR(I)*NU11(I))
              Q(I)  =ONE/(ONE+DR(I)*NU21(I))
              P2    =P(I)*P(I)
              Q2    =Q(I)*Q(I)
              F     =A(I)*P2+B(I)*Q2-YLD_I*YLD_I
              DF    =-(ANU1(I)*P2*P(I)+BNU2(I)*Q2*Q(I))
     .               *(YOUNG-DR(I)*H2(I))/YLD_I
     .                -H2(I)*YLD_I
              IF (DPLA_I(I) > ZERO) THEN
                DPLA_J(I)=MAX(ZERO,DPLA_I(I)-F/DF)
              ELSE
                DPLA_J(I)=ZERO
              ENDIF
            ENDDO ! DO J=1,NINDX
          ENDDO ! DO N=1,NMAX
        ENDIF ! FISOKIN > 0
C------------------------------------------
C     CONTRAINTES PLASTIQUEMENT ADMISSIBLES
C------------------------------------------
#include "vectorize.inc" 
        DO J=1,NINDX
          I=INDEX(J)
          PLA(I) = PLA(I) + DPLA_I(I)
          EPCHK(I) = MAX(PLA(I),EPCHK(I))
          S1=(SIGNXX(I)+SIGNYY(I))*P(I)
          S2=(SIGNXX(I)-SIGNYY(I))*Q(I)
          SIGNXX(I)=HALF*(S1+S2)
          SIGNYY(I)=HALF*(S1-S2)
          SIGNXY(I)=SIGNXY(I)*Q(I)
          EZZ(I) = DR(I)*S1/YOUNG
        ENDDO
C-------------------------
      ELSEIF (IPLA == 2) THEN
C-------------------------
C     von mises criterion
C-------------------------
        DO I = 1,NEL
          SVM2(I)= SIGNXX(I)*SIGNXX(I)
     .       +SIGNYY(I)*SIGNYY(I)
     .       -SIGNXX(I)*SIGNYY(I)
     .       +THREE*SIGNXY(I)*SIGNXY(I)
          SVM(I)=SQRT(SVM2(I))  
        END DO
C-------------------------
C     GATHER PLASTIC FLOW
C-------------------------
        NINDX=0
        DO I = 1,NEL
          YLD2(I)=YLD(I)*YLD(I)
          IF (SVM2(I) > YLD2(I) .AND. OFF(I) == ONE) THEN
            NINDX=NINDX+1
            INDEX(NINDX)=I
          END IF
        END DO
!
        IF (NINDX /= 0) THEN
C-------------
C   NORMAL PROJECTION TO THE CRITERION WITH APPROXIMATE CALCULATION OF THE NORMAL + RADIAL RETURN
C-------------
#include "vectorize.inc" 
          DO J=1,NINDX
            I=INDEX(J)
            IF (YLD(I) >= YMAX(I)) THEN
              H(I)=ZERO
            ELSE
              H(I)=CN*CB(I)*EXP((CN-ONE)*LOG(PLA(I)+SMALL))
            ENDIF
            ETSE(I)= H(I)/(H(I)+YOUNG)
C
            AA=(SVM2(I)-YLD2(I))
     .        /(FIVE*SVM2(I)+THREE*(-SIGNXX(I)*SIGNYY(I)+SIGNXY(I)*SIGNXY(I)))
            S1=(ONE-TWO*AA)*SIGNXX(I)+       AA*SIGNYY(I)
            S2=AA*SIGNXX(I)+(ONE-TWO*AA)*SIGNYY(I)
            S3=(ONE-THREE*AA)*SIGNXY(I)
            SIGNXX(I)=S1
            SIGNYY(I)=S2
            SIGNXY(I)=S3
            DPLA(I) = OFF(I)*(SVM(I)-YLD(I))/(THREE*G+H(I))
            PLA(I) = PLA(I) + DPLA(I)
C
            YLD(I) =YLD(I)+H(I)*DPLA(I)
          END DO ! DO J=1,NINDX
C
#include "vectorize.inc"
          DO J=1,NINDX
            I=INDEX(J)
            SVM(I)=SQRT( SIGNXX(I)*SIGNXX(I)
     .                  +SIGNYY(I)*SIGNYY(I)
     .                  -SIGNXX(I)*SIGNYY(I)
     .            +THREE*SIGNXY(I)*SIGNXY(I))
            R  = MIN(ONE,YLD(I)/MAX(EM20,SVM(I)))
            SIGNXX(I)=SIGNXX(I)*R
            SIGNYY(I)=SIGNYY(I)*R
            SIGNXY(I)=SIGNXY(I)*R
            EZZ(I) = DPLA(I) * HALF*(SIGNXX(I)+SIGNYY(I)) / YLD(I)
          END DO ! DO J=1,NINDX
        END IF ! IF (NINDX /= 0)
      ENDIF ! IF (IPLA == 0)
C------------------------------------------
C     for tangent matrix
C------------------------------------------
      IF (IMPL_S > 0) THEN
        IF (IKT > 0) THEN
          DO I = 1,NEL
            IF (DPLA(I) > ZERO) THEN
c ...... Parameter d(gama)
              PLA_I =PLA(I)
              BETA = ONE-FISOKIN
              YLD(I) =MIN(YMAX(I),CA(I)+BETA*CB(I)*EXP(CN*LOG(PLA_I)))
              GAMA_IMP(I)= THREE_HALF*DPLA(I)/YLD(I)
c ...... HK,HH---
              SIGNOR(I,4)=FISOKIN*H(I)
              SIGNOR(I,5)=(ONE-FISOKIN)*H(I)
c ...... Deviatoric stresses shifted by modified back stress ->ksi
              SIGNOR(I,1)=THIRD*(TWO*SIGNXX(I)-SIGNYY(I))
              SIGNOR(I,2)=THIRD*(TWO*SIGNYY(I)-SIGNXX(I))
              SIGNOR(I,3)=TWO*SIGNXY(I)
            ELSE
              GAMA_IMP(I) = ZERO
            END IF ! IF (DPLA(I) > ZERO)
          END DO
        END IF ! (IKT > 0) THEN
      END IF ! IF (IMPL_S > 0)
C------------------------------------------
C     ECROUISSAGE CINE
C------------------------------------------
      IF (FISOKIN > ZERO) THEN
        IF (IPLA == 1 )THEN
#include "vectorize.inc" 
          DO J=1,NINDX
            I=INDEX(J)
            PLA_I =PLA(I)
            BETA = ONE-FISOKIN
C------YLD, H(I) should not be updated----
            IF (PLA_I == ZERO) THEN
              YLD(I) =CA(I)
            ELSE
              YLD(I) =MIN(YMAX(I),CA(I)+BETA*CB(I)*EXP(CN*LOG(PLA_I)))
            ENDIF
          END DO ! DO J=1,NINDX
        END IF ! (IPLA == 1) THEN
        DO I = 1,NEL
          HKIN = FISOKIN*H(I)
          ALPHA = HKIN*DPLA(I)/YLD(I)
          SIGBAKXX(I) = SIGBAKXX(I) + ALPHA*SIGNXX(I)
          SIGBAKYY(I) = SIGBAKYY(I) + ALPHA*SIGNYY(I) 
          SIGBAKXY(I) = SIGBAKXY(I) + ALPHA*SIGNXY(I) 
C
          SIGNXX(I)=SIGNXX(I) + SIGBAKXX(I)
          SIGNYY(I)=SIGNYY(I) + SIGBAKYY(I)
          SIGNXY(I)=SIGNXY(I) + SIGBAKXY(I)
        ENDDO
      END IF ! FISOKIN > ZERO
C--------------------------------
C     HARDENING MODULUS
C--------------------------------
      HARDM(1:NEL) = H(1:NEL)
!-----------
      RETURN
      END
